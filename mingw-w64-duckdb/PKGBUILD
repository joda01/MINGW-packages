_realname=duckdb
pkgbase=mingw-w64-${_realname}-git
pkgname=${MINGW_PACKAGE_PREFIX}-${_realname}-git
pkgver=r8220.6d3586322
pkgrel=1
pkgdesc="An Embeddable Analytical Database (mingw-w64)"
arch=('x86_64')
license=('MIT')
url="https://www.duckdb.org/"
depends=("${MINGW_PACKAGE_PREFIX}-python3"
    "${MINGW_PACKAGE_PREFIX}-python-numpy"
    "${MINGW_PACKAGE_PREFIX}-python-pandas"
)
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-pybind11"
             "git")
checkdepends=("${MINGW_PACKAGE_PREFIX}-python-pytest")
source=("${_realname}"::"https://github.com/duckdb/duckdb.git")
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_realname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "$srcdir/$_realname"
  #${MINGW_PREFIX}/bin/python "scripts/amalgamation.py"
  sed -i "s/'nt'/'ntx'/" "$srcdir/$_realname/tools/pythonpkg/setup.py"

  ${MINGW_PREFIX}/bin/cmake \
    -S . -B "${srcdir}/build" \
    -G"MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX=${pkgdir}${MINGW_PREFIX} \
    -DMINGW_LIBS=${MINGW_PREFIX} \
    -DBUILD_PARQUET_EXTENSION=1 \
    -DBUILD_TESTING=0
}
build() {
  ${MINGW_PREFIX}/bin/cmake --build "${srcdir}/build"
  ${MINGW_PREFIX}/bin/python "$srcdir/$_realname/tools/pythonpkg/setup.py" build
}

package() {
  DESTDIR="${pkgdir}${MINGW_PREFIX}" ${MINGW_PREFIX}/bin/cmake --install "${srcdir}/build"

  # Command line and tools do not have a install() decl yet
  install -D -m755 "${srcdir}/build/duckdb.exe" "${pkgdir}${MINGW_PREFIX}/bin/duckdb.exe"
  
  install -D -m755 "${srcdir}/build/tools/sqlite3_api_wrapper/libsqlite3_api_wrapper_static.a" \
    "${pkgdir}${MINGW_PREFIX}/lib/libsqlite3_api_wrapper_static.a"
  
  install -D -m644 "$srcdir/$_realname/LICENSE" \
    "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"

  cd "$srcdir/$_realname/tools/pythonpkg"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build  
}
